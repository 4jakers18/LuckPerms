project.version = '4.2'

dependencies {
    compileOnly 'com.google.guava:guava:19.0'
    compileOnly 'org.checkerframework:checker-qual:2.5.5'
}

if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
    apply plugin: 'signing'

    javadoc {
        options.encoding = 'UTF-8'
        options.charSet = 'UTF-8'
        options.links(
                'https://google.github.io/guava/releases/21.0/api/docs/',
                'https://docs.oracle.com/javase/8/docs/api/'
        )
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier 'javadoc'
        from javadoc.destinationDir
    }

    task sourcesJar(type: Jar) {
        classifier 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar
        archives sourcesJar
    }

    signing {
        useGpgCmd()
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom {
                    project {
                        name 'LuckPerms API'
                        description 'An advanced permissions plugin for Bukkit/Spigot, BungeeCord, Sponge, Nukkit and Velocity.'
                        url 'https://github.com/lucko/LuckPerms'

                        licenses {
                            license {
                                name 'MIT'
                                url 'https://opensource.org/licenses/MIT'
                            }
                        }

                        developers {
                            developer {
                                name 'Luck'
                                email 'git@lucko.me'
                                url 'https://github.com/lucko'
                            }
                        }

                        scm {
                            connection 'scm:git:https://github.com/lucko/LuckPerms.git'
                            developerConnection 'scm:git:git@github.com:lucko/LuckPerms.git'
                            url 'https://github.com/lucko/LuckPerms'

                        }

                        ciManagement {
                            system 'Jenkins'
                            url 'https://ci.lucko.me/job/LuckPerms'
                        }

                        issueManagement {
                            system 'GitHub'
                            url 'https://github.com/lucko/LuckPerms/issues'
                        }
                    }
                }
            }
        }
    }
}
