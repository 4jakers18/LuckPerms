subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'

    group = 'me.lucko.luckperms'
    version = '4.3-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    jar {
        from '../LICENSE.txt'
    }

    def determinePatchVersion = {
        // get the name of the last tag
        def tagInfo = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = tagInfo
        }
        tagInfo = tagInfo.toString()

        if (!tagInfo.contains('-')) {
            return 0
        }
        return tagInfo.split("-")[1]
    }

    project.ext.majorVersion = '4'
    project.ext.minorVersion = '3'
    project.ext.patchVersion = determinePatchVersion()
    project.ext.fullVersion = project.ext.majorVersion + '.' + project.ext.minorVersion + '.' + project.ext.patchVersion

    repositories {
        //mavenLocal()
        mavenCentral()

        maven {
            name 'luck-repo'
            url 'https://repo.lucko.me/'
        }
    }
}
