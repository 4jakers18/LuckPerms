buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2' }
    }

    dependencies {
        classpath 'gradle.plugin.net.minecrell:licenser:0.4.1'
    }
}

defaultTasks 'licenseFormat', 'build'

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'net.minecrell.licenser'

    group = 'me.lucko.luckperms'
    version = '4.3-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    jar {
        from '../LICENSE.txt'
    }

    def determinePatchVersion = {
        // get the name of the last tag
        def tagInfo = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = tagInfo
        }
        tagInfo = tagInfo.toString()

        if (!tagInfo.contains('-')) {
            return 0
        }
        return tagInfo.split("-")[1]
    }

    project.ext.majorVersion = '4'
    project.ext.minorVersion = '3'
    project.ext.patchVersion = determinePatchVersion()
    project.ext.fullVersion = project.ext.majorVersion + '.' + project.ext.minorVersion + '.' + project.ext.patchVersion

    license {
        header = rootProject.file('HEADER.txt')
        include '**/*.java'
        newLine = true
    }

    repositories {
        //mavenLocal()
        mavenCentral()
        maven { url 'https://repo.lucko.me/' }
    }
}
